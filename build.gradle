/*
 * Copyright D3 Ledger, Inc. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.kotlin_version = '1.3.10'
    ext.ktor_version = '1.0.0'
    ext.spring_boot_version = '1.5.19.RELEASE'
    ext.springfoxSwagger2Version = '2.9.2'
    ext.springBootVersion = '2.1.3.RELEASE'
    ext.kotlinVersion = '1.3.10'
    ext.notary_version = '5402b8cbd93b5ed11e84ad940438e1c5585e0a6a'
    ext.d3EthVersion = 'ba7134be3a801eac681c0c25403da16043cd132c'

    repositories {
        mavenCentral()
        jcenter()
        // gradle plugins repository
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //Spring-boot
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

plugins {
    id 'jp.co.soramitsu.sora-plugin' version '0.1.2'
}

group = 'd3'
version = '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.sonarqube'

repositories {
    mavenCentral()
    // for Ktor
    jcenter()
    maven { url = 'https://dl.bintray.com/kotlin/ktor' }
    maven { url 'https://jitpack.io' }
}

mainClassName = "jp.co.soramitsu.bootstrap.BootstrapMain"
shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'
// name of jar file that the shadowJar plugin generates
shadowJar.archiveName = 'app.jar'

// sora-plugin configs
soramitsu {
    projectGroup = 'd3-deploy'
    docker {
        // docker tag
        tag = System.getenv("DOCKER_TAG")
        // jar file that is used in the generated Dockerfile
        jar = new File("build/libs/app.jar")
        // the rest in configured using env variables
    }
}

shadowJar {
    zip64 true
}


dependencies {
    compile("com.github.d3ledger.notary:notary-commons:$notary_version") {
        exclude group: 'com.github.soramitsu.chain-adapter'
        exclude group: 'io.github.microutils'
    }
    implementation("com.github.d3ledger.d3-eth:eth:$d3EthVersion") {
        exclude group: 'com.github.d3ledger.notary'
    }

    compile 'io.github.microutils:kotlin-logging:1.4.9'

    implementation 'com.github.hyperledger:iroha-java:6.1.0'

    // kotlin related
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    //Iroha-java related
    implementation('com.github.hyperledger.iroha-java:testcontainers:6.0.0') {
        exclude group: 'org.slf4j'
    }

    //Spring-boot
    implementation("org.springframework.boot:spring-boot-starter:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.hibernate'
    }
    implementation "io.springfox:springfox-swagger2:$springfoxSwagger2Version"
    implementation "io.springfox:springfox-swagger-ui:$springfoxSwagger2Version"

    // Changelog interface
    compile('com.github.d3ledger.bootstrap-changelog:changelog-interface:72b15aabbcfaa88176617444ea36a201b0b347b7') {
        exclude group: 'com.github.hyperledger.iroha-java'
    }
    compile 'io.grpc:protoc-gen-grpc-java:1.14.0'

    //TODO this is very dirty hack. This dependency must be removed after migration to newer version of Spring Boot
    // https://mvnrepository.com/artifact/org.hibernate/hibernate-validator

    implementation 'org.hibernate:hibernate-validator:6.0.14.Final'

    implementation "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"

    implementation "commons-logging:commons-logging:1.2"

    compile 'com.google.protobuf:protobuf-java-util:3.6.1'
    compile 'org.bitcoinj:bitcoinj-core:0.14.7'

    //Web3j
    compile 'org.web3j:crypto:4.1.1'
    compile 'org.web3j:core:4.1.1'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = 5.6
}

task runBootstrap(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
}

sonarqube {
    properties {
        property "sonar.projectKey", "bootstrap"
    }
}
